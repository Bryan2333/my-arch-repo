# Maintainer: BryanLiang <liangrui.ch at gmail dot com>

_pkgname=drawio-desktop
pkgname="${_pkgname}"-bryan
pkgver=26.2.15
pkgrel=1
pkgdesc="Diagram drawing application built on web technology"
arch=('x86_64')
url="https://www.drawio.com"
license=('Apache-2.0')
depends=(
    "bash"
    "gtk3"
    "libnotify"
    "libxss"
    "nss"
    "alsa-lib"
)
makedepends=(
    "git"
    "nvm"
    "libxcrypt-compat"
)
provides=('drawio-desktop')
conflicts=('drawio-desktop')
source=(
    "git+https://github.com/jgraph/drawio-desktop.git#tag=v$pkgver"
    "git+https://github.com/jgraph/drawio.git#tag=v$pkgver"
    "01-only-build-for-linux-x64.patch"
    "02-disable-auto-updates.patch"
    "launch-drawio"
)
sha256sums=(
    'SKIP'
    'SKIP'
    '535fe449d329790cd474a4e2d2caffae34694a9df0812b380c0f4cbe0067a9f7'
    '248a44b4830b846fb1802d7a71218405d888aca783750954643f5639ed9093b3'
    '7e7984cf831d7d8d970b2f217782e7b281a1492cacf92a8f3ce2dc73a24cb626'
)

_ensure_local_nvm() {
    # let's be sure we are starting clean
    which nvm >/dev/null 2>&1 && nvm deactivate && nvm unload
    export NVM_DIR="${srcdir}/.nvm"

    # The init script returns 3 if version specified
    # in ./.nvrc is not (yet) installed in $NVM_DIR
    # but nvm itself still gets loaded ok
    source /usr/share/nvm/init-nvm.sh || [[ $? != 1 ]]
}


prepare() {
    cd "${_pkgname}"
    git submodule init
    git config submodule.drawio.url "${srcdir}/drawio"
    git -c protocol.file.allow=always submodule update

    patch -Np1 < "${srcdir}/01-only-build-for-linux-x64.patch"
    patch -Np1 < "${srcdir}/02-disable-auto-updates.patch"

    _ensure_local_nvm

    nvm install lts/jod
    nvm use lts/jod

    npm install
}

build() {
    cd "${_pkgname}"

    _ensure_local_nvm
    nvm use lts/jod

    npm run release-linux 

    mkdir "${srcdir}/tmp"

    mv "dist/draw.io-amd64-${pkgver}.deb" "${srcdir}/tmp"

    cd "${srcdir}/tmp"

    ar x "draw.io-amd64-${pkgver}.deb"

    [[ -f "data.tar.xz" ]] && bsdtar -xf data.tar.xz

    rm -r "usr/share/doc"

    sed -i \
        -e "s|Exec=.*|Exec=/usr/bin/drawio %U|" \
        -e '/^StartupWMClass/d' \
        "usr/share/applications/drawio.desktop"

    mv "usr/share/applications/drawio.desktop" "usr/share/applications/draw.io.desktop"
}

package() {
    cd "${srcdir}/tmp"
    install -dm755 "${pkgdir}/opt"

    cp -r "opt/draw.io" "${pkgdir}/opt/drawio-desktop"
    cp -r "usr" "${pkgdir}/"

    install -Dm 755 "${srcdir}/launch-drawio" "${pkgdir}/usr/bin/drawio"
}

