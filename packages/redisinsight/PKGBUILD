# Maintainer: Dan Walsh <dan@danwalsh.ca>
pkgname=redisinsight
pkgver=2.64.1
pkgrel=2
pkgdesc="Desktop manager that provides an intuitive and efficient GUI for Redis, allowing you to interact with your databases, monitor, and manage your data."
arch=('x86_64')
url='https://redis.com/redis-enterprise/redis-insight'
license=('SSPL-1.0')
depends=()
provides=(redis-insight)
conflicts=(redis-insight-bin)
makedepends=(
gendesk
nodejs
yarn
npm
nvm
jq
make
python-setuptools
clang
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/RedisInsight/RedisInsight/archive/$pkgver.tar.gz")
sha256sums=('850ea1cb2dc9b6a77df253447e163c50ede91a59d7d8e6f0047f158593fcb2db')

source+=("launch-redisinsight")
sha256sums+=("2440c762fbfeadb4536a51b28de07159cac042aad42148be76a3134709675648")

_ensure_local_nvm() {
    # let's be sure we are starting clean
    which nvm >/dev/null 2>&1 && nvm deactivate && nvm unload
    export NVM_DIR="${srcdir}/.nvm"

    # The init script returns 3 if version specified
    # in ./.nvrc is not (yet) installed in $NVM_DIR
    # but nvm itself still gets loaded ok
    source /usr/share/nvm/init-nvm.sh || [[ $? != 1 ]]
}

prepare() {
  cd RedisInsight-$pkgver

  # unable to build some plugins for some reason, disable them here
  sed -i -E '/^# Build ri-explain plugin/,$d' scripts/build-statics.sh

  # automatically accept installing npx package
  echo "$(jq '.scripts.postinstall = "npx --yes patch-package"' redisinsight/package.json)" > redisinsight/package.json

  _ensure_local_nvm
  nvm install 20.12.2
}

build() {
  cd RedisInsight-$pkgver
  _ensure_local_nvm

  mkdir electron
  VERSION=$(jq -r ".version" redisinsight/package.json)
  cp ./redisinsight/package.json ./electron/package.json
  echo "$VERSION" > electron/version

  yarn --cwd redisinsight/api/ install --ignore-optional
  yarn --cwd redisinsight/ install --ignore-optional
  yarn install
  yarn build:statics
  yarn build:prod
  yarn electron-builder build --dir -p never
}

package() {
  cd "$srcdir/RedisInsight-$pkgver/release/linux-unpacked"
  install -Dm644 resources/resources/logo.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/redisinsight.svg"
  gendesk -f -n --pkgname "${pkgname}" \
          --pkgdesc "$pkgdesc" \
          --name "RedisInsight" \
          --comment "$pkgdesc" \
          --exec "${pkgname}" \
          --categories 'Development' \
          --icon "${pkgname}"
  install -Dm644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm644 resources/LICENSE.redisinsight.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d "${pkgdir}/usr/bin"
  install -d "${pkgdir}/opt"
  install -d "${pkgdir}/opt/${pkgname}"
  cp -avR * "${pkgdir}/opt/${pkgname}"
  install -Dm755 "${srcdir}/launch-redisinsight" "${pkgdir}/usr/bin/redisinsight"
  find "${pkgdir}/opt/${pkgname}" -type d -exec chmod 755 {} +
}
